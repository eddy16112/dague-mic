extern "C" %{
/*
 *  Copyright (c) 2010      
 * 
 *  The University of Tennessee and The University
 *  of Tennessee Research Foundation.  All rights
 *  reserved.
 *
 * @precisions normal z -> s d c
 *
 */
#define PRECISION_z

#include <plasma.h>
#include <core_blas.h>


#include "dague.h"
#include "data_distribution.h"
#include "data_dist/matrix/precision.h"
#include "data_dist/matrix/matrix.h"
#include "dplasma/lib/dplasmajdf.h"

%}

/*
 * Globals
 */
uplo   [type = PLASMA_enum]
bump   [type = dague_complex64_t]
seed   [type = "unsigned long long int" ]
descA  [type = "tiled_matrix_desc_t"]
A      [type = "dague_ddesc_t *"]

plasma_upper   [type="PLASMA_enum" hidden=on default=PlasmaUpper ]
plasma_lower   [type="PLASMA_enum" hidden=on default=PlasmaLower ]

PLGSY_L(m, n)  [profile = off]

// Execution space
m = 1 .. ((uplo == plasma_upper) ? 0 : descA.mt-1)
n = 0 .. ( m < descA.nt ? m-1 : descA.nt-1 )

// Parallel partitioning
: A(m, n)

// Parameters

RW A <- A(m, n)
     -> A(m, n)
BODY

    int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
    int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
    int ldam = BLKLDD( descA, m );

#if !defined(DAGUE_DRY_RUN)
    CORE_zplgsy(bump, tempmm, tempnn, A, ldam,
                descA.m, m*descA.mb, n*descA.nb, seed );
#endif  /* !defined(DAGUE_DRY_RUN) */

    printlog("plgsy_l( %d, %d )\n"
             "    ( %f, %d, %d, A(%d,%d), %d, %d, %d, %d, %llu)\n",
             m, n, creal(bump), tempmm, tempnn, 
             m, n, ldam,
             descA.m, m*descA.mb, n*descA.nb, seed);
END

PLGSY_U(m, n)  [profile = off]

// Execution space
m = 0   .. descA.mt-1
n = m+1 .. ((uplo == plasma_lower) ? 0 : descA.nt-1)

// Parallel partitioning
: A(m, n)

// Parameters

RW   A <- A(m, n)
       -> A(m, n)
BODY

    int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
    int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
    int ldam = BLKLDD( descA, m );

#if !defined(DAGUE_DRY_RUN)
    CORE_zplgsy(bump, tempmm, tempnn, A, ldam,
                descA.m, m*descA.mb, n*descA.nb, seed );
#endif  /* !defined(DAGUE_DRY_RUN) */

    printlog("plgsy_u( %d, %d )\n"
             "    ( %f, %d, %d, A(%d,%d), %d, %d, %d, %d, %llu)\n",
             m, n, creal(bump), tempmm, tempnn, 
             m, n, ldam,
             descA.m, m*descA.mb, n*descA.nb, seed);

END

PLGSY_DIAG(k)  [profile = off]

// Execution space
     k = 0 .. ( descA.mt < descA.nt ? descA.mt-1 : descA.nt-1 )

// Parallel partitioning
: A(k, k)

// Parameters

RW   A <- A(k, k)
       -> A(k, k)
BODY

    int tempkm = ((k)==((descA.mt)-1)) ? ((descA.m)-(k*(descA.mb))) : (descA.mb);
    int tempkn = ((k)==((descA.nt)-1)) ? ((descA.n)-(k*(descA.nb))) : (descA.nb);
    int ldak = BLKLDD( descA, k );

#if !defined(DAGUE_DRY_RUN)
    CORE_zplgsy(bump, tempkm, tempkn, A, ldak,
                descA.m, k*descA.mb, k*descA.nb, seed );
#endif  /* !defined(DAGUE_DRY_RUN) */

    printlog("plgsy_diag( %d )\n"
             "    ( %f, %d, %d, A(%d,%d), %d, %d, %d, %d, %llu)\n",
             k, creal(bump), tempkm, tempkn, 
             k, k, ldak,
             descA.m, k*descA.mb, k*descA.nb, seed);

END
extern "C" %{

%}
